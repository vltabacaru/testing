# Develop Mobile Apps using Oracle REST Data Services

## Introduction

dbNotify demo mobile application requires Oracle REST Data Services (ORDS) to communicate with your Oracle database. ORDS is preconfigured and available with all Oracle Autonomous Database instances, you just need to enable it. You can use Oracle Application Express (APEX) to enable and manage RESTful Web Services, and probably this is the easiest way. Oracle APEX also comes preconfigured and available with all Oracle Autonomous Database instances.

![](./images/guide19.jpg "")

Estimated Lab Time: 90 minutes

### Objectives
In this lab, you will:
* Create an APEX workspace
* Register APEX Schema with ORDS
* Create REST services and database objects
* Use ORDS into iOS and Android mobile applications
* Use APEX applications for backend management

### Prerequisites
* This lab requires completion of the Provision Autonomous Database lab in the Contents menu on the left.

## Task 1: Prepare APEX environment 

1. On Autonomous Database Details page, under Tools section. Click **Open APEX**.

   ![](./images/guide00.jpg "")

2. If you already have existing Workspaces created, use the Administration Services link, to access APEX instance management, and the password for the Autonomous Database ADMIN user to login.

   ![](./images/guide01.jpg "")

3. For this demo, it is mandatory to create a new Workspace.

   ![](./images/guide02.jpg "")

4. This workspace requires a certain Database User name and Workspace Name. Use `DBNOTIFY21` for both Database User name and Workspace Name. When setting the Password, make sure it is a strong one.

   ![](./images/guide03.jpg "")

5. Sign out from Administration Services console, and sign into your new DBNOTIFY21 workspace.

   ![](./images/guide04.jpg "")

6. Navigate to SQL Workshop > **RESTful Services**. This is the ORDS management console.

   ![](./images/guide05.jpg "")

7. You should see a notification message saying `Schema not registered with ORDS`. Click **Register Schema with ORDS** button.

   ![](./images/guide06.jpg "")

8. These are the default values for ORDS Schema Attributes. They are just fine, you can click **Save Schema Attributes** button.

   ![](./images/guide07.jpg "")

9. If everything is right, you will receive a confirmation message:
`Schema enabled for use with ORDS RESTful Services and sample RESTful Service successfully installed.`

## Task 2: Create database objects

1. Now you can access SQL Workshop > **SQL Commands**, to build the required RESTful Web Services.

   ![](./images/guide08.jpg "")

    >**Note** : If you want to speed up this lab, by skipping the steps that explain REST services and database objects, jump to **step 26** and run the SQL script to create them all in one go.

    >**Recommendation** : Read these steps to understand the database objects required by a simple mobile application to communicate with an Oracle Autonomous Database via ORDS, and execute only **step 26** in SQL Commands to create them all.

2. Create a table to store alert messages.

    ````
    create table "MOBILE_ALERTS" 
        ("ID" NUMBER GENERATED BY DEFAULT ON NULL AS IDENTITY MINVALUE 1 MAXVALUE 9999999999999999999999999999 
          INCREMENT BY 1 START WITH 1 CACHE 20 NOT NULL ENABLE, 
        "MSG_TEXT" VARCHAR2(4000 CHAR) NOT NULL ENABLE, 
        "CREATED" DATE NOT NULL ENABLE, 
        "ACCEPTED" DATE, 
        "ACCEPTED_BY" VARCHAR2(120 CHAR), 
        "SOLVED" DATE,
        "SOLVED_BY" VARCHAR2(120 CHAR), 
        CONSTRAINT "MOBILE_ALERTS_ID_PK" PRIMARY KEY ("ID")
      );
    ````

3. Create a trigger to insert date and time values in the messages table.

    ````
    create or replace trigger "MOBILE_ALERTS_BIU"
        before insert or update
        on MOBILE_ALERTS
        for each row
      begin
        if inserting then
          :new.created := SYSDATE;
        end if;
        if :old.accepted_by is null and :new.accepted_by is not null then
          :new.accepted := SYSDATE;
        end if;
        if :old.solved_by is null and :new.solved_by is not null then
          :new.solved := SYSDATE;
        end if;
      end;
    /
    ````

4. Create a trigger that will send to the mobile application any new message inserted into our table.

    ````
    create or replace TRIGGER "MOBILE_ALERTS_AI"
        after insert
        on MOBILE_ALERTS
        for each row
      declare
        vcRequestBody clob;
        vcTopic varchar2(32);
      begin
        SELECT SUBSTR(sys_context('USERENV','DB_NAME'), 0, INSTR(sys_context('USERENV','DB_NAME'), '_')-1) 
          INTO vcTopic FROM dual;
        vcTopic := lower(vcTopic);
    
        apex_web_service.g_request_headers(1).name := 'Content-Type';
        apex_web_service.g_request_headers(1).value := 'application/json'; 
    
        apex_web_service.g_request_headers(2).name := 'Authorization';
        apex_web_service.g_request_headers(2).value := 'key=AAAA271MvR8:APA91bHBR9_ixZYCgoccYnFtQboMXFSqEqLY' ||
                                                       '88GbQvTQ__asZWRPkSrdDaXLa2MW3ogsnMUIytx2BIqgwyt2UxhD' ||
                                                       'IcKyfBEa8oewmkrtLzZQITn0fa7h37VR2Xr_--SZEoe0vN6VtFBb';
    
        vcRequestBody := apex_web_service.make_rest_request(
            p_url => 'https://fcm.googleapis.com/fcm/send',
            p_http_method => 'POST',
            p_body => '{"to":"/topics/' || vcTopic || '","data":{"data_key":"' || :new.msg_text || '"}}');
    
        vcRequestBody := apex_web_service.make_rest_request(
            p_url => 'https://fcm.googleapis.com/fcm/send',
            p_http_method => 'POST',
            p_body => '{"to":"/topics/' || vcTopic || '_iOS","content_available":true,"mutable_content":true' ||
                      ',"priority":"high","notification":{"title":"-","body":"-","sound":"default"},"data":{"data_value":"' ||
                      :new.msg_text || '","count_id":' || to_char(:new.id) || '}}');
      end;
    /
    ````

5. Create the RESTful service that returns solved messages from our table.

    ````
    begin
      ords.create_service(
          p_module_name => 'alert_web_service',
          p_base_path  => '/alerts/',
          p_pattern =>  'all_alerts/',
          p_items_per_page => 10,
          p_source  =>  'select ID, MSG_TEXT, to_char(CREATED, ''DD/MM/YYYY HH24:MI:SS'') CREATED, 
                         to_char(ACCEPTED, ''DD/MM/YYYY HH24:MI:SS'') ACCEPTED, ACCEPTED_BY, 
                         to_char(SOLVED, ''DD/MM/YYYY HH24:MI:SS'') SOLVED, SOLVED_BY from MOBILE_ALERTS 
                         where SOLVED is not null ORDER BY ID DESC');
    end;
    /
    ````

6. Define a resource template that has a matching pattern based on the message ID.

    ````
    begin
      ords.define_template(
          p_module_name => 'alert_web_service',
          p_pattern => 'all_alerts/:id');
    end;
    /
    ````

7. Define a module handler that returns one message by ID.

    ````
    begin
      ords.define_handler(
          p_module_name => 'alert_web_service',
          p_pattern => 'all_alerts/:id',
          p_method => 'GET',
          p_items_per_page => 10,
          p_source  =>  'select ID, MSG_TEXT, to_char(CREATED, ''DD/MM/YYYY HH24:MI:SS'') CREATED, 
                         to_char(ACCEPTED, ''DD/MM/YYYY HH24:MI:SS'') ACCEPTED, ACCEPTED_BY, 
                         to_char(SOLVED, ''DD/MM/YYYY HH24:MI:SS'') SOLVED, SOLVED_BY from MOBILE_ALERTS 
                         where ID = :id');
    end;
    /
    ````

8. Define a resource template that will be used to return messages that are not solved.

    ````
    begin
      ords.define_template(
          p_module_name => 'alert_web_service',
          p_pattern => 'open_alerts/');
    end;
    /
    ````

9. Define a module handler that returns the messages that are not solved.

    ````
    begin
      ords.define_handler(
          p_module_name => 'alert_web_service',
          p_pattern => 'open_alerts/',
          p_method => 'GET',
          p_items_per_page => 10,
          p_source  =>  'select ID, MSG_TEXT, to_char(CREATED, ''DD/MM/YYYY HH24:MI:SS'') CREATED, 
                         to_char(ACCEPTED, ''DD/MM/YYYY HH24:MI:SS'') ACCEPTED, ACCEPTED_BY, 
                         to_char(SOLVED, ''DD/MM/YYYY HH24:MI:SS'') SOLVED, SOLVED_BY from MOBILE_ALERTS 
                         where SOLVED is null ORDER BY ID DESC');
    end;
    /
    ````

10. Create an Oracle REST Data Services role.

    ````
    begin
      ords.create_role('alert_web_service-admin');
    end;
    /
    ````

11. Create an Oracle REST Data Services privilege.

    ````
    begin  
      ords.create_privilege(
          p_name => 'alert_web_service-priv',
          p_role_name => 'alert_web_service-admin',
          p_label => 'Mobile Web Service',
          p_description => 'Provide access to mobile web service data');
    end;
    /
    ````

12. Associate the privilege with resources.

    ````
    begin
      ords.create_privilege_mapping(
          p_privilege_name => 'alert_web_service-priv',
          p_pattern => '/alerts/*');
    end;
    /
    ````

13. Create an OAuth client registration.

    ````
    begin
      oauth.create_client(
          p_name => 'alert_web_service Mobile Web Service Credentials',
          p_grant_type => 'client_credentials',
          p_privilege_names => 'alert_web_service-priv',
          p_support_email => 'support@example.com');
    end;
    /
    ````

14. Grant an OAuth client the specified role, enabling clients performing two-legged OAuth to access privileges requiring the role.

    ````
    begin
      oauth.grant_client_role(
         'alert_web_service Mobile Web Service Credentials',
         'alert_web_service-admin');
    end;
    /
    ````

15. Define a resource template that will be used to accept messages from the mobile application.

    ````
    begin
      ords.define_template(
          p_module_name => 'alert_web_service',
          p_pattern => 'accept_alert/');
    end;
    /
    ````

16. Define a module handler that will receive the request from the mobile application to accept a message, returning the corresponding status.

    ````
    begin
      ords.define_handler(
          p_module_name => 'alert_web_service',
          p_pattern => 'accept_alert/',
          p_method => 'POST',
          p_source_type => ords.source_type_plsql,
          p_source => 'BEGIN update MOBILE_ALERTS set ACCEPTED_BY = :vusername where ID = :vid; 
                       EXCEPTION when others then :vstatus := 400; END;');
    end;
    /
    ````

17. Define a parameter for accept message module handler. The name of the parameter is used by the mobile application in the HTTP Header. The bind variable name is referred to in the SQL statement. The access method indicates the parameter is an input value. This parameter is used to send the user name from the mobile application to the database.

    ````
    begin
      ords.define_parameter(
          p_module_name => 'alert_web_service',
          p_pattern => 'accept_alert/',
          p_method => 'POST',
          p_name => 'USER_NAME',
          p_bind_variable_name => 'vusername',
          p_source_type => 'HEADER',
          p_param_type => 'STRING',
          p_access_method => 'IN');
    end;
    /
    ````

18. Define a second parameter for accept message module handler, to send the message ID from the mobile application to the database.

    ````
    begin
      ords.define_parameter(
          p_module_name => 'alert_web_service',
          p_pattern => 'accept_alert/',
          p_method => 'POST',
          p_name => 'ALERT_ID',
          p_bind_variable_name => 'vid',
          p_source_type => 'HEADER',
          p_param_type => 'STRING',
          p_access_method => 'IN');
    end;
    /
    ````

19. Define a third parameter for accept message module handler. The access method indicates the parameter is an output value. This parameter is used to send the request status from the database to the mobile application.

    ````
    begin
      ords.define_parameter(
          p_module_name => 'alert_web_service',
          p_pattern => 'accept_alert/',
          p_method => 'POST',
          p_name => 'X-APEX-STATUS-CODE',
          p_bind_variable_name => 'vstatus',
          p_source_type => 'HEADER',
          p_param_type => 'INT',
          p_access_method => 'OUT');
    end;
    /
    ````

20. Define a resource template that will be used to solve messages from the mobile application.

    ````
    begin
      ords.define_template(
          p_module_name => 'alert_web_service',
          p_pattern => 'solve_alert/');
    end;
    /
    ````

21. Define a module handler that will receive the request from the mobile application to solve a message, returning the corresponding status.

    ````
    begin
      ords.define_handler(
          p_module_name => 'alert_web_service',
          p_pattern => 'solve_alert/',
          p_method => 'POST',
          p_source_type => ords.source_type_plsql,
          p_source => 'BEGIN update MOBILE_ALERTS set SOLVED_BY = :vusername where ID = :vid; 
                       EXCEPTION when others then :vstatus := 400; END;');
    end;
    /
    ````

22. Define a parameter for solve message module handler. The name of the parameter is used by the mobile application in the HTTP Header. The bind variable name is referred to in the SQL statement. The access method indicates the parameter is an input value. This parameter is used to send the user name from the mobile application to the database.

    ````
    begin
      ords.define_parameter(
          p_module_name => 'alert_web_service',
          p_pattern => 'solve_alert/',
          p_method => 'POST',
          p_name => 'USER_NAME',
          p_bind_variable_name => 'vusername',
          p_source_type => 'HEADER',
          p_param_type => 'STRING',
          p_access_method => 'IN');
    end;
    /
    ````

23. Define a second parameter for solve message module handler, to send the message ID from the mobile application to the database.

    ````
    begin
      ords.define_parameter(
          p_module_name => 'alert_web_service',
          p_pattern => 'solve_alert/',
          p_method => 'POST',
          p_name => 'ALERT_ID',
          p_bind_variable_name => 'vid',
          p_source_type => 'HEADER',
          p_param_type => 'STRING',
          p_access_method => 'IN');
    end;
    /
    ````

24. Define a third parameter for solve message module handler. The access method indicates the parameter is an output value. This parameter is used to send the request status from the database to the mobile application.

    ````
    begin
      ords.define_parameter(
          p_module_name => 'alert_web_service',
          p_pattern => 'solve_alert/',
          p_method => 'POST',
          p_name => 'X-APEX-STATUS-CODE',
          p_bind_variable_name => 'vstatus',
          p_source_type => 'HEADER',
          p_param_type => 'INT',
          p_access_method => 'OUT');
    end;
    /
    ````

25. You can verify that the client was registered and has requested access to the `alert_web_service-priv` privilege by querying the `USER_ORDS_CLIENTS` view. The `CLIENT_ID` and `CLIENT_SECRET` values represent the secret credentials for the OAuth client. These values must be noted and kept secure. You can think of them as the userid and password for the client application.

    ````
    select CLIENT_ID, CLIENT_SECRET from USER_ORDS_CLIENTS 
        where NAME = 'alert_web_service Mobile Web Service Credentials';
    ````

26. Copy the code from [dbNotifyScript.sql](https://objectstorage.eu-zurich-1.oraclecloud.com/p/RHLAHUAUpU4TNciN1g7Sn4HgZcotNp_ffaA2Ls7MXC-TVTS7rA8K4TfowUC5Ugkw/n/zrzzjaap2wvk/b/web-files/o/dbNotifyScript.sql) and run it in SQL Commands.

27. Make sure you don't miss any line, or there are no extra characters. Click **Run** button.

   ![](./images/guide09.jpg "")

28. If everything is correct, you will get an output similar to this:

    >**Example** : 
    ````
    Client ID = dzNFsdDeJS_J-jhADZBqMB..
    Client Secret = WL-Dr8Us9lftgdEkf-c6sl..
    ````

29. Save Client ID and Client Secret values, including the two dots at the end, you will need these strings in the dbNotify mobile application. These are the credentials to your RESTful Web Service, make sure you keep them in a safe place.

30. From SQL Workshop > **RESTful Services**, copy and paste `alert_web_service` URL in your notes (next to Client ID and Client Secret values), removing `alerts/` at the end. This URL is also needed in the dbNotify mobile application login.

    >**Example** : 
    ````
    alert_web_service URL = https://xxxx9xxxxxx27x4-adbname.adb.eu-frankfurt-1.oraclecloudapps.com/ords/dbnotify21/
    ````

   ![](./images/guide10.jpg "")

## Task 3: Use ORDS into dbNotify mobile demo application

1. Install dbNotify application on your mobile phone: [App Store](https://apps.apple.com/us/app/dbnotify/id1583780865) or [Google Play](https://play.google.com/store/apps/details?id=com.hls.dbnotify).

2. Send Client ID, Client Secret and `alert_web_service` URL values to your mobile phone using notes or your email client. 

3. Launch dbNotify mobile application, choose a username, and copy/paste Client ID, Client Secret and `alert_web_service` URL in the last three fields. Click `Login`.

   ![](./images/guide12.jpg "")

4. At this moment you have no alerts in your application. Alerts can be generated by inserting any text in `MSG_TEXT` column of `MOBILE_ALERTS` table from `DBNOTIFY21` schema.

   ![](./images/guide13.jpg "")

5. Use App Builder Import tool to import [MobileAlerts.sql](https://objectstorage.eu-zurich-1.oraclecloud.com/p/RHLAHUAUpU4TNciN1g7Sn4HgZcotNp_ffaA2Ls7MXC-TVTS7rA8K4TfowUC5Ugkw/n/zrzzjaap2wvk/b/web-files/o/MobileAlerts.sql) Mobile Alerts application in your Application Express instance. 

6. Launch Mobile Alerts Apex application, and create your first alert.

   ![](./images/guide11.jpg "")

8. When a new message is inserted in `MOBILE_ALERTS` table, this generates an alert on your mobile phone.

   ![](./images/guide14.jpg "")

9. Open the alert in dbNotify mobile application.

   ![](./images/guide15.jpg "")

10. Message is truncated, if you want to see the entire message click view icon on the right (eye icon).

   ![](./images/guide18.jpg "")

11. An alert can have three stages: new, accepted, and solved. To accept an alert, click **Accept**.

   ![](./images/guide16.jpg "")

12. After being accepted, to solve an alert, click **Solve**. Once solved, the alert message goes into History.

   ![](./images/guide17.jpg "")

### Current limitations

* One dbNotify application can be used per cloud tenancy.
* Message text is up to 4000 characters long.


## Acknowledgements

- **Author** - Valentin Leonard Tabacaru
- **Last Updated By/Date** - Valentin Leonard Tabacaru, Principal Product Manager, DB Product Management, Sep 2021

## Need Help?

Please submit feedback or ask for help using our [LiveLabs Support Forum](https://community.oracle.com/tech/developers/categories/livelabsdiscussions). Please click the **Log In** button and login using your Oracle Account. Click the **Ask A Question** button to the left to start a *New Discussion* or *Ask a Question*.  Please include your workshop name and lab name.  You can also include screenshots and attach files.  Engage directly with the author of the workshop.

If you do not have an Oracle Account, click [here](https://profile.oracle.com/myprofile/account/create-account.jspx) to create one.
